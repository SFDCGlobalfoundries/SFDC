/**
* @description This class is use to handle ws integration between PLM to MRS for DRW Complete signal
* @history 
*       - 17-Aug-2015: created
*       - 9-Oct-2015: add prime die list to layer
*/
global class MRSPLMDRWService {

    global class PlmDrwPTRF {
        webservice String messageID;//Alphanumeric Unique identifier for the call out. Generated by source system: PLM
        webservice String ptrfNumber;//Unique PTRF identifier
        webservice list<PlmDrwLayer> layers;//list of layers under source ptrf
    }
    
    global class PlmDrwLayer {
        webservice string maskLayerNumber;//Mask Layer number in a PTRF
        webservice string maskRev;//Revision for the Mask Layer
        webservice list<PlmDrwPrimeDie> primeDies;//list of prime die under this layer
    }
    
    global class PlmDrwPrimeDie {
        webservice string primeDie;
    }
    
    /*
    * @description called by other systems (PLM) to complete DRW by layer of ptrf
    * @param PlmDrwPTRF msg
    * @return
    */
    webservice static String callService(PlmDrwPTRF msg) {
        
        //swgp-1109 : On/Off Switches for MRS/DRT
        Environment_Variable__c env = Environment_Variable__c.getInstance('SWIFT_SWITCH_MANTIS_PLM_DRWR');
        if(null != env && env.value__c != 'ON') throw new MRSWebserviceException('We are sorry!!! 503 Service Unavailable. The server is currently unavailable (because it is overloaded or down for maintenance).');
        //=======================================================
        
        Environment_Variable__c switchValue = Environment_Variable__c.getInstance('SWIFT_PLM_DRWR_SWITCH'); 
        Boolean isServiceOn = (null != switchValue && switchValue.Value__c == 'ON') ? true : false; // if switch off, incoming request will be fail
        
        if(isServiceOn) {
            MRSPLMDRWServiceHandler.fetch().handleRequest(json.serialize(msg));
            return 'SUCCESS';//ack signal, always success
        } else {
            return 'ERROR: Service not found';//service off, return temp message
        }
    }
}